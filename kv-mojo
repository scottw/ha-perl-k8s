#!/usr/bin/env perl
use Mojolicious::Lite;
use Mojo::Redis2;

helper redis => sub { shift->stash->{redis} ||= Mojo::Redis2->new; };

helper redis_response => sub {
    my $c = shift;
    return sub {
        my ($d, $err, $value) = @_;
        $c->res->headers->header('kv-mojo-version' => $ENV{KV_VERSION} // 'v1');

        $c->app->log->warn("Error: $err") if $err;

        $value //= '';
        $c->render(text => $value);
    }
};

get '/:key' => sub {
    my $c = shift;
    $c->render_later;

    $c->delay(
        sub {
            $c->redis->get($c->param('key'), shift->begin);
        },

        $c->redis_response
    );
};

put '/:key' => sub {
    my $c = shift;
    $c->render_later;

    $c->delay(
        sub {
            $c->redis->set($c->param('key'), $c->req->body, shift->begin);
        },

        $c->redis_response
    );
};

del '/:key' => sub {
    my $c = shift;
    $c->render_later;

    $c->delay(
        sub {
            $c->redis->del($c->param('key'), shift->begin);
        },

        $c->redis_response
    );
};

post '/die' => sub { kill TERM => $$ };

app->start;
